plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.github.spotbugs' version '4.7.1'
    id 'org.owasp.dependencycheck' version '6.1.6'
    id 'com.google.cloud.tools.jib' version '3.0.0'
}

group = 'com.example'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'

test {
    useJUnitPlatform()
}

dependencyCheck {
    failBuildOnCVSS = 8
    suppressionFile = file('depcheck-suppressions.xml')
    format = 'HTML'
    analyzers {
        msbuildEnabled = false
        assemblyEnabled = false
        nuspecEnabled = false
        composerEnabled = false
        swiftEnabled = false
        nodeEnabled = false
    }
}

spotbugsMain {
    reports {
        html {
            enabled = true
        }
        xml {
            enabled = false
        }
    }
}

jib {
    to {
        image = imageName
    }
    container {
        user = 1002
    }
}

check.dependsOn dependencyCheckAnalyze
tasks.build.dependsOn tasks.jib

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.owasp.encoder:encoder:1.2.3'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    spotbugs 'com.github.spotbugs:spotbugs:4.2.3'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
